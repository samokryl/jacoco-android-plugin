plugins {
	id 'org.jetbrains.kotlin.jvm' version '1.4.20'
	id 'com.gradle.plugin-publish' version '0.12.0'
	id 'java-gradle-plugin'
}

apply from: 'dependencies.gradle'

group 'ru.samokryl.jacoco.android'
version '0.1.0'

repositories {
	mavenCentral()
	google()
}

sourceSets {
	integrationTest {
		kotlin {
			srcDir file('src/functionalTest/kotlin')
		}
		resources {
			srcDir file('src/functionalTest/resources')
		}

		compileClasspath += sourceSets.main.output + configurations.testRuntimeClasspath
		runtimeClasspath += output + compileClasspath
	}
}

task integrationTest(type: Test)  {
	description = 'Run the integration test.'
	group = 'verification'

	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath

	mustRunAfter(tasks.named('test'))
}

check.dependsOn integrationTest

dependencies {
	implementation deps.stdLib
	implementation deps.reflect
	implementation gradleApi()
	implementation deps.buildTools

	testImplementation deps.junit
	testImplementation gradleTestKit()

	integrationTestImplementation(project)
	integrationTestImplementation gradleTestKit()
	integrationTestImplementation deps.junit
}

gradlePlugin {
	plugins {
		jacocoAndroid {
			id = 'ru.samokryl.jacoco.android'
			implementationClass = 'ru.samokryl.jacoco.android.JacocoPlugin'
		}
	}

	testSourceSets sourceSets.integrationTest
}

pluginBundle {
	website = 'http://samokryl.ru'
	vcsUrl = 'https://github.com/samokryl/jacoco-android-plugin'
	description = 'The jacoco plugin for android multi-module projects'

	plugins {
		jacocoAndroid {
			displayName = 'Jacoco Android plugin'
			tags = ['jacoco']
			version = project.version
		}
	}
}